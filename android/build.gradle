buildscript {
  ext.getExtOrDefault = { name ->
    return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties["Cas_" + name]
  }

  repositories { google(); mavenCentral() }
  dependencies {
    classpath "com.android.tools.build:gradle:8.7.2"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${getExtOrDefault('kotlinVersion')}"
  }
}

apply plugin: "com.android.library"
apply plugin: "org.jetbrains.kotlin.android"   
apply plugin: "com.facebook.react"             

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["Cas_" + name]).toInteger()
}

android {
  namespace "com.cas"

  compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")

  defaultConfig {
    minSdkVersion getExtOrIntegerDefault("minSdkVersion")
    targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")
    consumerProguardFiles "consumer-rules.pro"
  }

  buildFeatures { buildConfig true }

  buildTypes { release { minifyEnabled false } }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_17
    targetCompatibility JavaVersion.VERSION_17
  }
  kotlinOptions { jvmTarget = "17" }

  sourceSets {
    main {
      java.srcDirs += ["generated/java", "generated/jni"]
    }
  }

  lint { disable += ["GradleCompatible"] }
}

react {
  libraryName = "Cas"
  codegenJavaPackageName = "com.cas"
}

repositories {
  google()
  mavenCentral()
  maven { url 'https://sdk.cleveradssolutions.com/repo' }
}

def kotlin_version = getExtOrDefault("kotlinVersion")

dependencies {
  implementation "com.facebook.react:react-android"
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  compileOnly "com.cleveradssolutions:cas-sdk:4.3.0"
  implementation 'androidx.preference:preference-ktx:1.2.1'
}