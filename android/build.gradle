buildscript {
  ext.getExtOrDefault = { name ->
    rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties["Cas_" + name]
  }
  repositories { google(); mavenCentral() }
  dependencies {
    classpath "com.android.tools.build:gradle:8.7.2"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${getExtOrDefault('kotlinVersion')}"
  }
}

def isNewArchitectureEnabled() {
  return rootProject.hasProperty("newArchEnabled") && rootProject.getProperty("newArchEnabled") == "true"
}

apply plugin: "com.android.library"
apply plugin: "org.jetbrains.kotlin.android"
if (isNewArchitectureEnabled()) {
  apply plugin: "com.facebook.react"
}

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["Cas_" + name]).toInteger()
}

android {
  namespace "com.cleveradssolutions.plugin.reactnative"

  sourceSets {
    main {
      manifest.srcFile "src/main/AndroidManifestNew.xml"
    }
  }

  compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")

  defaultConfig {
    minSdkVersion getExtOrIntegerDefault("minSdkVersion")
    targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")
    consumerProguardFiles "consumer-rules.pro"
    buildConfigField("boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchitectureEnabled().toString())
  }

  buildFeatures { buildConfig true }

  sourceSets {
    main {
      if (isNewArchitectureEnabled()) {
        java.srcDirs += ['src/newarch']
      } else {
        java.srcDirs += ['src/oldarch']
      }
    }
  }

  buildTypes { release { minifyEnabled false } }
  compileOptions { sourceCompatibility JavaVersion.VERSION_17; targetCompatibility JavaVersion.VERSION_17 }
  kotlinOptions { jvmTarget = "17" }
  lint { disable += ["GradleCompatible"] }
}

repositories {
  google()
  mavenCentral()
  maven { url 'https://sdk.cleveradssolutions.com/repo' }
}

dependencies {
  implementation "com.facebook.react:react-android"

  implementation "org.jetbrains.kotlin:kotlin-stdlib:${getExtOrDefault('kotlinVersion')}"
  compileOnly "com.cleveradssolutions:cas-sdk:4.3.0"
  implementation 'androidx.preference:preference-ktx:1.2.1'
}
